plugins {
    id 'java'
    id 'scala'
    id 'idea'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
}
group = 'me.skymc'
version = '4.86'

sourceCompatibility = 1.8
targetCompatibility = 1.8

defaultTasks 'build', 'shadowJar'

tasks.withType(ScalaCompile) {
    options.encoding = 'UTF-8'
    scalaCompileOptions.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
    maven { url "http://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {
    shadow(group: 'org.apache.cassandra', name: 'cassandra-all', version: '0.8.1') {
        exclude(module: 'slf4j-log4j12')
        exclude(module: 'log4j')
    }
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.8'
    shadow group: 'com.zaxxer', name: 'HikariCP', version: '3.1.0'
    shadow group: 'org.javalite', name: 'activejdbc', version: '2.0'
    compile group: 'org.ow2.asm', name: 'asm', version: '7.0-beta'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.7'
    shadow group: 'com.h2database', name: 'h2', version: '1.4.197'
    shadow group: 'me.clip', name: 'placeholderapi', version: '2.8.4'
    shadow group: 'net.objecthunter', name: 'exp4j', version: '0.4.8'
    shadow group: 'org.scala-lang', name: 'scala-library', version: '2.12.8'
    shadow fileTree(dir: 'libs', includes: ['*.jar'])
}

shadowJar {
    // 免得 MANIFEST.MF 里面刷一堆 lib 难看
    taskActions.removeIf { it.actionClassName.contains 'configureShadowTask' }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand 'version': project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}